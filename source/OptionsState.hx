package;

import flixel.ui.FlxBar;
import flixel.FlxG;
import flixel.FlxState;
import flixel.text.FlxText;
import flixel.ui.FlxButton;
import flixel.util.FlxAxes;
import flixel.util.FlxColor;

class OptionsState extends FlxState
{
    var titleText:FlxText;
    var volumeBar:FlxBar;
    var volumeText:FlxText;
    var volumeAmountText:FlxText;
    var volumeDownButton:FlxButton;
    var volumeUpButton:FlxButton;
    var clearDataButton:FlxButton;
    var backButton:FlxButton;
    #if desktop
    var fullscreenButton:FlxButton;
    #end

    override public function create():Void
    {
        titleText = new FlxText(0, 20, 0, "Options", 22);
        titleText.alignment = CENTER;
        titleText.screenCenter(FlxAxes.X);
        add(titleText);

        volumeText = new FlxText(0, titleText.y + titleText.height + 10, 0, "Volume", 8);
        volumeText.alignment = CENTER;
        volumeText.screenCenter(FlxAxes.X);
        add(volumeText);

        volumeDownButton = new FlxButton(8, volumeText.y + volumeText.height + 2, "-", clickVolumeDown);
        volumeDownButton.loadGraphic(AssetPaths.button__png, true, 20, 20);
        volumeDownButton.onUp.sound = FlxG.sound.load(AssetPaths.select__wav);
        add(volumeDownButton);

        volumeUpButton = new FlxButton(FlxG.width - 28, volumeDownButton.y, "+", clickVolumeUp);
        volumeUpButton.loadGraphic(AssetPaths.button__png, true, 20, 20);
        volumeUpButton.onUp.sound = FlxG.sound.load(AssetPaths.select__wav);
        add(volumeUpButton);
        
        volumeBar = new FlxBar(volumeDownButton.x + volumeDownButton.width + 4, volumeDownButton.y, LEFT_TO_RIGHT, Std.int(FlxG.width - 64), Std.int(volumeUpButton.height));
        volumeBar.createFilledBar(0xff464646, FlxColor.WHITE, true, FlxColor.WHITE);
        add(volumeBar);

        volumeAmountText = new FlxText(0, 0, 200, (FlxG.sound.volume * 100) + "%", 8);
        volumeAmountText.alignment = CENTER;
        volumeAmountText.borderStyle = FlxTextBorderStyle.OUTLINE;
        volumeAmountText.borderColor = 0xff464646;
        volumeAmountText.y = volumeBar.y + (volumeBar.height / 2) - (volumeAmountText.height / 2);
        add(volumeAmountText);

        #if desktop
        fullscreenButton = new FlxButton(0, volumeBar.y + volumeBar.height + 8, FlxG.fullscreen ? "FULLSCREEN" : "WINDOWED", clickFullscreen);
        fullscreenButton.screenCenter(FlxAxes.X);
        add(fullscreenButton);
        #end

        clearDataButton = new FlxButton((FlxG.width / 2) - 90, FlxG.height - 28, "Clear Data", clickClearData);
        clearDataButton.onUp.sound = FlxG.sound.load(AssetPaths.select__wav);
        add(clearDataButton);

        backButton = new FlxButton((FlxG.width / 2) - 90, FlxG.height - 28, "Back", clickBack);
        backButton.onUp.sound = FlxG.sound.load(AssetPaths.select__wav);
        add(backButton);

        updateVolume();

        FlxG.camera.fade(FlxColor.BLACK, 0.33, true);

        super.create();
    }

    #if desktop
    function clickFullscreen()
    {
        FlxG.fullscreen = !FlxG.fullscreen;
        fullscreenButton.text = FlxG.fullscreen ? "FULLSCREEN" : "WINDOWED";
        FlxG.save.data.fullscreen = FlxG.fullscreen;
    }
    #end

    function clickClearData()
    {
        FlxG.save.erase();
        FlxG.sound.volume = 0.5;
        updateVolume();
    }

    function clickBack(){
        FlxG.save.flush();
        FlxG.camera.fade(FlxColor.BLACK, 0.33, false, function(){
            FlxG.switchState(MenuState.new);
        });
    }
    
    function clickVolumeDown()
    {
        FlxG.sound.volume -= 0.1;
        FlxG.save.data.volume = FlxG.sound.volume;
        updateVolume();
    }

    function clickVolumeUp()
    {
        FlxG.sound.volume += 0.1;
        FlxG.save.data.volume = FlxG.sound.volume;
        updateVolume();
    }

    function updateVolume()
    {
        var volume:Int = Math.round(FlxG.sound.volume * 100);
        volumeBar.value = volume;
        volumeAmountText.text = volume + "%";
    }
}